#######################################################################
# Test for copying block of size 4;
#######################################################################
	.pos 0
main:	irmovq Stack, %rsp  	# Set up stack pointer

	# Set up arguments for copy function and then invoke it
	irmovq $4, %rdx		# src and dst have 4 elements
	irmovq dest, %rsi	# dst array
	irmovq src, %rdi	# src array
	call ncopy		 
	halt			# should halt with num nonzeros in %rax
StartFun:
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - 8-way unrolled version for Y86 architecture
# Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Name: 徐文博
# ID: 1320240207
##################################################################

# %rdi = src, %rsi = dst, %rdx = len
ncopy:
##################################################################
# init
    xorq %rax, %rax          # global count = 0
    andq %rdx, %rdx
    jle Done

##################################################################
# 8-way loop
##################################################################
Loop8_Check:
    rrmovq %rdx, %rbx        # rbx = len
    iaddq $-8, %rbx          # rbx = len - 8
    jl Remain                # if len < 8 -> remainder

Loop8:
    # ---- load 8 ----
    mrmovq  0(%rdi), %r8
    mrmovq  8(%rdi), %r9
    mrmovq 16(%rdi), %r10
    mrmovq 24(%rdi), %r11
    mrmovq 32(%rdi), %r12
    mrmovq 40(%rdi), %r13
    mrmovq 48(%rdi), %rbx
    mrmovq 56(%rdi), %rcx

    # ---- store 8 ----
    rmmovq %r8,  0(%rsi)
    rmmovq %r9,  8(%rsi)
    rmmovq %r10, 16(%rsi)
    rmmovq %r11, 24(%rsi)
    rmmovq %r12, 32(%rsi)
    rmmovq %r13, 40(%rsi)
    rmmovq %rbx, 48(%rsi)
    rmmovq %rcx, 56(%rsi)

    # ---- local count (reduce rax dependency) ----
    xorq %r14, %r14          # local_count = 0

    andq %r8, %r8            # Check r8
    jle L0
    iaddq $1, %r14
L0:
    andq %r9, %r9            # Check r9
    jle L1
    iaddq $1, %r14
L1:
    andq %r10, %r10          # Check r10
    jle L2
    iaddq $1, %r14
L2:
    andq %r11, %r11          # Check r11
    jle L3
    iaddq $1, %r14
L3:
    andq %r12, %r12          # Check r12
    jle L4
    iaddq $1, %r14
L4:
    andq %r13, %r13          # Check r13
    jle L5
    iaddq $1, %r14
L5:
    andq %rbx, %rbx          # Check rbx
    jle L6
    iaddq $1, %r14
L6:
    andq %rcx, %rcx          # Check rcx
    jle L7
    iaddq $1, %r14
L7:

    # ---- merge local count ----
    addq %r14, %rax

    # ---- update pointers & len ----
    iaddq $64, %rdi          # src += 8 * 8
    iaddq $64, %rsi          # dst += 8 * 8
    iaddq $-8, %rdx          # len -= 8
    jmp Loop8_Check

##################################################################
# remainder loop (1-way)
##################################################################
Remain:
    andq %rdx, %rdx
    jle Done

Loop1:
    mrmovq 0(%rdi), %r10
    rmmovq %r10, 0(%rsi)

    andq %r10, %r10
    jle R0
    iaddq $1, %rax
R0:
    iaddq $8,  %rdi
    iaddq $8,  %rsi
    iaddq $-1, %rdx
    jg Loop1

##################################################################
Done:
    ret
##################################################################
End:
#/* $end ncopy-ys */
EndFun:

###############################
# Source and destination blocks 
###############################
	.align 8
src:
	.quad -1
	.quad 2
	.quad 3
	.quad -4
	.quad 0xbcdefa # This shouldn't get moved

	.align 16
Predest:
	.quad 0xbcdefa
dest:
	.quad 0xcdefab
	.quad 0xcdefab
	.quad 0xcdefab
	.quad 0xcdefab
Postdest:
	.quad 0xdefabc

.align 8
# Run time stack
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0

Stack:
