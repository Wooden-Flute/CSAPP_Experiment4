#######################################################################
# Test for copying block of size 4;
#######################################################################
	.pos 0
main:	irmovq Stack, %rsp  	# Set up stack pointer

	# Set up arguments for copy function and then invoke it
	irmovq $4, %rdx		# src and dst have 4 elements
	irmovq dest, %rsi	# dst array
	irmovq src, %rdi	# src array
	call ncopy		 
	halt			# should halt with num nonzeros in %rax
StartFun:
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# Name:徐文博
# ID:1320240207
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		# count = 0;
	andq %rdx,%rdx		# len <= 0?
	jle Done		# if so, goto Done:

###################################################################
# 4-way循环展开
###################################################################

Loop4_Check:
	rrmovq %rdx, %rbx  # rbx = len
	iaddq $-4, %rbx   # rbx = len - 4
	jl Remain        # if len < 4 goto handle remainder

Loop4:
	#-------load 4 values from src-------
	mrmovq (%rdi), %r10      # load val0
	mrmovq 8(%rdi), %r11     # load val1
	mrmovq 16(%rdi), %r12    # load val2
	mrmovq 24(%rdi), %r13    # load val3

	#-------store 4 values to dst-------
	rmmovq %r10, (%rsi)        # store val0
	rmmovq %r11, 8(%rsi)      # store val1
	rmmovq %r12, 16(%rsi)     # store val2
	rmmovq %r13, 24(%rsi)     # store val3

	#-------check positives-------
	andq %r10, %r10       # val0 <= 0?
	jle L0_Npos		 # if so, goto L0_Npos
	iaddq $1, %rax       # count++

L0_Npos:
	andq %r11, %r11       # val1 <= 0?
	jle L1_Npos		 # if so, goto L1_Npos
	iaddq $1, %rax       # count++

L1_Npos:
	andq %r12, %r12 	 # val2 <= 0?
	jle L2_Npos		 # if so, goto L2_Npos
	iaddq $1, %rax    # count++

L2_Npos:
	andq %r13, %r13 	 # val3 <= 0?
	jle L3_Npos      # if so, goto L3_Npos
	iaddq $1, %rax    # count++

L3_Npos:
	#-------update pointers and counters-------	
	iaddq $32, %rdi  # src += 4 * 8
	iaddq $32, %rsi  # dst += 4 * 8
	iaddq $-4, %rdx  # len -= 4
	jmp Loop4_Check

###################################################################
# handle remainder (len < 4)
Remain:
	andq %rdx,%rdx		# len <= 0?
	jle Done		# if so, goto Done:

Loop1:
	mrmovq (%rdi), %r10	# read val from src...
	rmmovq %r10, (%rsi)	# ...and store it to

	andq %r10, %r10		# val <= 0?
	jle Rpos		# if so, goto Rpos:
	iaddq $1, %rax  # count++
Rpos:
	iaddq $8, %rdi  # src++
	iaddq $8, %rsi  # dst++
	iaddq $-1, %rdx  # len--
	jg Loop1			# if so, goto Loop1:

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
EndFun:

###############################
# Source and destination blocks 
###############################
	.align 8
src:
	.quad -1
	.quad 2
	.quad -3
	.quad 4
	.quad 0xbcdefa # This shouldn't get moved

	.align 16
Predest:
	.quad 0xbcdefa
dest:
	.quad 0xcdefab
	.quad 0xcdefab
	.quad 0xcdefab
	.quad 0xcdefab
Postdest:
	.quad 0xdefabc

.align 8
# Run time stack
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0

Stack:
